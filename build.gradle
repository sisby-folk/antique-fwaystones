//file:noinspection GroovyAssignabilityCheck
//file:noinspection GroovyAccessibility
buildscript {
	repositories {
		gradlePluginPortal()
	}
	dependencies {
		classpath 'com.modrinth.minotaur:Minotaur:2.4.3'
		classpath "com.github.breadmoirai:github-release:2.4.1"
	}
}

plugins {
	id 'maven-publish'
	alias libs.plugins.loom
}

if (System.getenv("MODRINTH_TOKEN")) {
	apply plugin: "com.modrinth.minotaur"
}
if (System.getenv("GITHUB_TOKEN")) {
	apply plugin: "com.github.breadmoirai.github-release"
}

archivesBaseName = project.archives_base_name

def mc_major = '1.19'
def mc = mc_major + '.2'
def default_branch = '1.19'

def origVersion = project.version
project.version = project.version + "+" + mc_major
group = project.maven_group

repositories {
	maven { url "https://maven.terraformersmc.com/" }
	maven { url "https://maven.cafeteria.dev/releases" }
	maven { url "https://api.modrinth.com/maven" }
	maven { url "https://maven.wispforest.io" }
	maven { url "https://maven.shedaniel.me/" } // Cloth Config
	maven { url 'https://jitpack.io' }
	mavenLocal()
}

dependencies {
	// // Base // //
	minecraft libs.mc
	mappings variantOf(libs.qm) { classifier "intermediary-v2" }
	modImplementation libs.ql
	modImplementation libs.qfapi

	// Compat //
	modImplementation libs.antiqueatlas
	modImplementation(libs.fwaystones) {
		exclude module: 'journeymap-32274'
		exclude module: 'journeymap-api'
	}
}

processResources {
	final Map<String, String> map = [
		"version"        : version.toString(),
		"mc"             : libs.versions.mc.get(),
		"ql"             : libs.versions.ql.get(),
		"fapi"          : libs.versions.qfapi.get(),
		"qfapi"          : libs.versions.qfapi.get(),
		"antiqueatlas"          : libs.versions.antiqueatlas.get(),
		"fwaystones"          : libs.versions.fwaystones.get(),
	]

	inputs.properties(map)
	filesMatching('*.mod.json') { expand(map) }
}

tasks.withType(JavaCompile).configureEach {
	it.options.encoding = "UTF-8"
	it.options.release = 17
}

java {
	withSourcesJar()
}

jar {
	from("LICENSE.txt") {
		rename { "${it}_${archivesBaseName}" }
	}
}

publishing {
	publications {
		mavenJava(MavenPublication) {
			from components.java
		}
	}

	repositories {

	}
}

if (System.getenv("MODRINTH_TOKEN")) {
	modrinth {
		token = System.getenv("MODRINTH_TOKEN")
		projectId = 'antique-fwaystones'
		versionNumber = project.version
		versionName = origVersion
		versionType = 'release'
		uploadFile = remapJar
		gameVersions = [mc]
		loaders = ['quilt', 'fabric']
		detectLoaders = false
		dependencies {
			required.project "fwaystones"
			required.project "antique-atlas"
		}
		changelog = "Changelog: https://github.com/sisby-folk/antique-fwaystones/releases/tag/v" + origVersion
		syncBodyFrom = rootProject.file("README.md").text
	}
}

if (System.getenv("GITHUB_TOKEN")) {
	githubRelease {
		token System.getenv("GITHUB_TOKEN")
		owner "sisby-folk"
		repo "antique-fwaystones"
		tagName 'v' + origVersion
		releaseName origVersion
		targetCommitish default_branch
		draft false
		prerelease false
		releaseAssets remapJar.archiveFile
		allowUploadToExisting true
		generateReleaseNotes true
	}
	tasks.githubRelease.dependsOn remapJar
}
